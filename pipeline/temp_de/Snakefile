import os
import pandas as pd

configfile: "../../private_config.yaml"

cell_types = config['cell_types']
cellranger_versions = ['v2', 'v3']
pseudobulk = ['TRUE', 'FALSE']

# Get samples we need
design_df = pd.read_csv(config['sample_inventory_url']).dropna()

design_df = design_df[design_df.sample_type != "patient"]

sce_ids=list(design_df['id'])

sces_qc = expand("../../data/scesets/{cv}/{id}_sceset_{cv}_qc.rds",
                    id=sce_ids, cv=cellranger_versions)



primary_tumour_csvs = expand("../../data/primary_tumour_temp_de/{cv}/DE_results_{ct}_pseudobulk_{pb}.csv",
                             cv=cellranger_versions,
                             ct=cell_types,
                             pb=pseudobulk)

pt_figs = expand("../../figs/primary_tumour_temp_de/{fn}_{cv}_pseudobulk_{pb}.png",
                 fn = ['volcano','grid'],
                 cv = cellranger_versions,
                 pb = pseudobulk)

pdx_cl_de_csvs = expand("../../data/pdx_cl_temp_de/{cv}/DE_results_pseudobulk_{pb}.csv",
                             cv=cellranger_versions,
                             pb=pseudobulk)

pdx_cl_pathway_csvs = expand("../../data/pdx_cl_temp_de/{cv}/pathway_results_pseudobulk_{pb}.csv",
                             cv=cellranger_versions,
                             pb=pseudobulk)


pdx_cl_figs = expand("../../figs/pdx_cl_temp_de/{fn}_{cv}_pseudobulk_{pb}.png",
                 fn = ['volcano','grid','pathway'],
                 cv = cellranger_versions,
                 pb = pseudobulk)


rule all:
    input:
        primary_tumour_csvs,
        pt_figs,
        pdx_cl_de_csvs,
        pdx_cl_pathway_csvs,
        pdx_cl_figs


rule pdx_cl_de:
    params:
        curr_dir=os.getcwd()
    input:
        sces_qc
    output:
        de_csv="../../data/pdx_cl_temp_de/{cv}/DE_results_pseudobulk_{pb}.csv",
        pathway_csv="../../data/pdx_cl_temp_de/{cv}/pathway_results_pseudobulk_{pb}.csv",
        report="../../reports/pdx_cl_temp_de/pdx_cl_temp_de_{cv}_pseudobulk_{pb}.html"
    shell:
        "Rscript -e \"rmarkdown::render('pdx_cl_temp_de.Rmd', \
        output_file='{params.curr_dir}/{output.report}', \
        knit_root_dir='{params.curr_dir}', \
        params=list(cellranger_version='{wildcards.cv}', \
        pseudobulk='{wildcards.pb}',\
        de_csv='{output.de_csv}',\
        pathway_csv='{output.pathway_csv}'))\" "    


rule pdx_cl_figs:
    params:
        curr_dir=os.getcwd()
    input:
        de_csv="../../data/pdx_cl_temp_de/{cv}/DE_results_pseudobulk_{pb}.csv",
        pathway_csv="../../data/pdx_cl_temp_de/{cv}/pathway_results_pseudobulk_{pb}.csv",
    output:
        volcano="../../figs/pdx_cl_temp_de/volcano_{cv}_pseudobulk_{pb}.png",
        grid="../../figs/pdx_cl_temp_de/grid_{cv}_pseudobulk_{pb}.png",
        pathway="../../figs/pdx_cl_temp_de/pathway_{cv}_pseudobulk_{pb}.png",
        report="../../reports/pdx_cl_temp_de/collated_report_{cv}_pseudobulk_{pb}.html"
    shell:
        "Rscript -e \"rmarkdown::render('pdx_cl_results.Rmd', \
        output_file='{params.curr_dir}/{output.report}', \
        knit_root_dir='{params.curr_dir}', \
        params=list(cellranger_version='{wildcards.cv}', \
        de_csv='{input.de_csv}',\
        pathway_csv='{input.pathway_csv}',\
        volcano_plot='{output.volcano}',\
        grid_plot='{output.grid}',\
        pathway_plot='{output.pathway}',\
        pseudobulk='{wildcards.pb}'))\" "    

rule primary_tumour_de:
    params:
        curr_dir=os.getcwd()
    input:
        sce="../../data/primary_tumour_analysis/v1/sce_final_annotated/{cv}.rds"
    output:
        csv="../../data/primary_tumour_temp_de/{cv}/DE_results_{ct}_pseudobulk_{pb}.csv",
        report="../../reports/primary_tumour_temp_de/primary_tumour_de_{cv}_{ct}_pseudobulk_{pb}.html"
    shell:
        "Rscript -e \"rmarkdown::render('primary_tumour_temp_de.Rmd', \
        output_file='{params.curr_dir}/{output.report}', \
        knit_root_dir='{params.curr_dir}', \
        params=list(cellranger_version='{wildcards.cv}', \
        cell_type='{wildcards.ct}', \
        input_sce='{input.sce}', \
        pseudobulk='{wildcards.pb}',\
        output_csv='{output.csv}'))\" "    

rule primary_tumour_figs:
    params:
        curr_dir=os.getcwd()
    input:
        csvs=expand("../../data/primary_tumour_temp_de/{{cv}}/DE_results_{ct}_pseudobulk_{{pb}}.csv",
                    ct=cell_types)
    output:
        volcano="../../figs/primary_tumour_temp_de/volcano_{cv}_pseudobulk_{pb}.png",
        grid="../../figs/primary_tumour_temp_de/grid_{cv}_pseudobulk_{pb}.png",
        report="../../reports/primary_tumour_temp_de/collated_report_{cv}_pseudobulk_{pb}.html"
    shell:
        "Rscript -e \"rmarkdown::render('primary_tumour_results.Rmd', \
        output_file='{params.curr_dir}/{output.report}', \
        knit_root_dir='{params.curr_dir}', \
        params=list(cellranger_version='{wildcards.cv}', \
        volcano_plot='{output.volcano}',\
        grid_plot='{output.grid}',\
        pseudobulk='{wildcards.pb}'))\" "    
